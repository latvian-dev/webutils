import java.time.Instant

plugins {
	id 'java'
	id 'maven-publish'
}

def ENV = System.getenv()
version = "${project_version}-${ENV.GITHUB_RUN_NUMBER ? 'build.' + ENV.GITHUB_RUN_NUMBER : 'local.' + Instant.now().epochSecond}"
archivesBaseName = 'webutils'
group = 'dev.latvian.apps'

repositories {
	mavenCentral()

	maven {
		url "https://maven.latvian.dev/releases"
		content {
			includeGroup "dev.latvian.apps"
		}
	}
}

dependencies {
	implementation('net.dv8tion:JDA:6.2.0')
	compileOnly('dev.latvian.apps:ansi:1.0.0-build.12')
	implementation('dev.latvian.apps:tiny-java-server:1.0.0-build.34')
	implementation('org.mongodb:mongodb-driver-sync:5.6.2')
	compileOnly('org.jetbrains:annotations:24.0.1')

	testImplementation('dev.latvian.apps:ansi:1.0.0-build.12')
	testImplementation('org.junit.jupiter:junit-jupiter-api:5.10.0')
	testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.10.0')
	testImplementation('junit:junit:4.13.2')
}

jar {
	manifest {
		attributes 'Implementation-Version': archiveVersion
	}
}

test {
	useJUnitPlatform()
}

compileJava {
	options.encoding = "UTF-8"
	options.release.set(21)
	options.compilerArgs << '-parameters' << '-Xmaxerrs' << '1000'
}

java {
	sourceCompatibility = targetCompatibility = '21'
	withSourcesJar()
	// withJavadocJar()
}

publishing {
	publications {
		mavenIchor(MavenPublication) {
			artifactId = 'webutils'
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_TOKEN) {
			maven {
				url "https://maven.latvian.dev/releases"
				credentials {
					username = "lat"
					password = "${ENV.MAVEN_TOKEN}"
				}
			}
		}
	}
}